options {
	JAVA_UNICODE_ESCAPE=true;
	STATIC=false;					// 1 seul parseur actif a la fois (+ rapide)
	MULTI=true;						// type noeud = f(nom noeud)
	VISITOR=true;					// Cree une methode accept(visiteur)
}

//
// PARSER DEFINITION
//

PARSER_BEGIN(Parser)

package analyzer.ast;

public class Parser
{
	public static ASTProgram ParseTree(java.io.InputStream input) throws ParseException
	{
		Parser c = new Parser(input);
		return c.Program();
	}
}

PARSER_END(Parser)

//
// LEXICAL ANALYSIS
//

// White space

SKIP :
{
	< ( " " | "\t" | "\n" | "\r" )+	>
}

// Comments

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}




// Keywords
TOKEN :
{
	< IF: "if"	> |
	< THEN: "then"	> |
	< ELSE: "else" > |
	< WHILE: "while" > |

	< BOOLEAN : <TRUE> | <FALSE> > |
	< #TRUE: "true" > |
	< #FALSE: "false" > |
	< MATCH: "match" > |
	< WITH: "with" > |
	< DO: "do" > |
	< INPUT: "INPUT" > |
	< OUTPUT: "OUTPUT" >

}

// Operators
TOKEN :
{
	<ASSIGN: < EQUAL >  > |
	<COMPARE: < LESS > | < LESSEQUAL > | < GREAT > | <GREATEQUAL > | < DIFF > | < EQUALEQUAL > > |
    <MULOP: <FOIS> | <DIV> | <MOD>> |
    <BOOLOP : <AND> | <OR> > |

    //plus and minus doivent être séparer puisque Minus peut être utilisé seul.
    < PLUS: "+" > |
	< MINUS: "-" > |

	< #EQUAL: "=" > |

	< #LESS: "<" > |
	< #LESSEQUAL: "<=" > |
	< #GREAT: ">" > |
	< #GREATEQUAL: ">=" > |
	< #DIFF: "!=" > |
	< #EQUALEQUAL: "==" > |


	< #FOIS: "*" > |
	< #DIV: "/" > |
	< #MOD: "%" > |
	< #POW: "**" >|
    < #AND: "&&" > |
    < #OR: "||" > |

    < NOT: "!" > |

	< LPAREN: "(" > |
	< RPAREN: ")" > |
	< LACC: "{" > |
	< RACC: "}" > |
	< COLON: ":" > |
	< ENDSTMT : ";" > |
	< COMMA: "," >
	// À compléter au besoin
}


// Identifiers and numbers
TOKEN :
{
	< IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
	< #LETTER: ["a"-"z","A"-"Z","_"] > |
	< #DIGIT: ["0"-"9"] > |
	< INTEGER: ["1"-"9"] (["0"-"9"])* | "0" > |
	// // Caution! Allows numbers such as "0000.23"
	< REAL: (<INTEGER>)+ "." (["0"-"9"])+ (<EXPONENT>)? |
			  (<INTEGER>)+ <EXPONENT> > |
	< #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
}







//
// SYNTAX ANALYSIS
//

ASTProgram Program() : { }
{
	Block() <EOF> { return jjtThis;  }
}

void Block() : { }
{
	 ( Stmt() | <LACC> Block() <RACC>)*
}

void Stmt() : { }
{
	AssignStmt() <ENDSTMT> |
	IfStmt() |
	WhileStmt() |
	<ENDSTMT>
}


void IfStmt() : {}
{
    //TODO à finir
     <IF> <LPAREN> Expr() <RPAREN>
     ( Stmt() | <LACC> Block() <RACC> [<ELSE> (<LACC> Block() <RACC> | Stmt() )])
}

void WhileStmt() : {}
{
    //TODO à finir
     <WHILE> <LPAREN> Expr() <RPAREN>
     ( Stmt() | <LACC> Block() <RACC>)
}

void AssignStmt() : {}
{
	Identifier() <ASSIGN> Expr()
}


void Expr() : {Token t;}
{
    //TODO à compléter.
    //TODO Vous pouvez modifier le contenu de cette règle et de toute les règles aillant "Expr dans leur nom",
    //TODO et ajouter de nouvelle règles.
    //TODO pour simplifier la correction, garder "Expr" à la fin de toutes les nouvelles règles crées pour
    //TODO définir la grammaire des expressions.
    (CompExpr()) (<BOOLOP> CompExpr() )*
}

void MulExpr() : { Token t;}
{
    GenValue() ((<MULOP>) GenValue())*
}

void AddExpr() : { Token t;}
{
    MulExpr() ((<PLUS> | <MINUS>) MulExpr())*
}

void CompExpr() : { Token t;}
{
    AddExpr() ((<COMPARE>) AddExpr())*
}

void GenValue() : {}
{
	[<MINUS>] ((<NOT>)* (BoolValue() | Identifier()| IntValue())) |
	(<LPAREN> Expr() <RPAREN>)

}

void BoolValue() : { Token t;}
{
    t = <BOOLEAN>  {jjtThis.setValue(t.image);}
}


void Identifier() : { Token t;}
{
	t = <IDENTIFIER> {jjtThis.setValue(t.image);}
}

void IntValue() : { Token t;}
{
	t = <INTEGER> {jjtThis.setValue(Integer.parseInt(t.image));}
}



